20 CLS
30 LOCATE0,0:PRINT"HANGMAN Version 1.0";
40 LOCATE0,1:PRINT"Autor: Manfred Becker, 17.05.94"
100 CLS:INPUT"Bitte geben Sie Ihren Text ein:";I$
110 CLS:A$="":B$="":FORI=1TO31:B$=B$+CHR$(160):NEXTI
120 PAUSE=250
125 VERSUCHE=9
130 FORI=1TOLEN(I$)
132 IFMID$(I$,I,1)=" "THENA$=A$+" "ELSE A$=A$+"-"
134 NEXTI
140 LOCATE0,0:PRINTA$;
150 LOCATE0,1:PRINT"Machen Sie nun Ihre Eingabe   ";
155 FORI=0TO PAUSE:NEXTI
160 LOCATE0,1:PRINTB$;
200 C$=INKEY$:IFC$=""ORC$=" "THEN200
210 FOUND=0:FORI=1TO LEN(B$)
212 IFMID$(B$,I,1)=C$ THENFOUND=1
214 NEXTI
220 IFFOUND=0THEN300
230 LOCATE0,1:PRINT"Buchstabe "+C$+" gab's schon mal!";
240 FORI=0TO PAUSE:NEXTI
250 GOTO140
300 FOUND=0:FOR I=1TO LEN(I$)
302 IFMID$(I$,I,1)=C$THENFOUND=1
304 NEXTI
310 I=0
312 I=I+1:IF(MID$(B$,I,1)<C$)THEN312
314 B$=LEFT$(B$,I-1)+C$+MID$(B$,I,31-I)
320 IFFOUND=1THEN500
400 REM Eingabe nicht vorhanden.
410 VERSUCHE=VERSUCHE-1
420 LOCATE0,1:PRINT"Buchstabe "+C$+" kommt nicht vor.";
430 FORI=0TO PAUSE:NEXTI
440 IFVERSUCHE=0THEN700
450 LOCATE0,1:PRINT"Sie haben noch";VERSUCHE;" Versuche.";
460 FORI=0TO PAUSE:NEXTI
470 GOTO140
500 REM Eingabe vorhanden.
510 Z=0:E=1:FORI=1TOLEN(I$)
520 IFMID$(I$,I,1)=C$THENZ=Z+1:A$=LEFT$(A$,I-1)+C$+MID$(A$,I+1,LEN(A$)-I)
530 IFMID$(A$,I,1)="-"THENE=0
540 NEXTI
550 LOCATE0,1:PRINTC$;" kommt";Z;"mal vor.           ";
560 FORI=0TO2*PAUSE:NEXTI
570 IFE=0THEN140
600 REM gewonnen...
610 LOCATE0,0:PRINTI$;
620 FORM=1TO5
630 LOCATE0,1:PRINT"                               ";
640 FORI=0TOPAUSE:NEXTI
650 LOCATE0,1:PRINT"HURRA!!! Sie haben gewonnen!   ";
660 FORI=0TOPAUSE:NEXTI
670 NEXTM
680 GOTO100
700 REM verloren...
710 LOCATE0,1:PRINT"Sie haben leider verloren!     ";
720 FORI=0TO2*PAUSE:NEXTI
730 LOCATE0,1:PRINT"Das war die richtige Loesung!  ";
740 FORI=0TO2*PAUSE:NEXTI
750 LOCATE0,0:PRINTI$;
760 FORI=0TO5*PAUSE:NEXTI
770 GOTO100
